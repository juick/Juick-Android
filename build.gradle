buildscript {
    ext.kotlin_version = "1.9.10"
    ext.navigation_version = "2.6.0"
    ext.okhttp_version = "3.12.13"
    ext.retrofit_version = "2.6.4"
    ext.glide_version = "4.16.0"
    ext.androidx_test_version = "1.5.0"

    repositories {
        mavenCentral()
        google()
        maven { url 'https://developer.huawei.com/repo/' }
    }
    dependencies {
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigation_version"
        classpath 'com.android.tools.build:gradle:8.1.2'
        classpath 'com.google.gms:google-services:4.4.0'
        classpath 'com.huawei.agconnect:agcp:1.9.1.301'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.devtools.ksp:com.google.devtools.ksp.gradle.plugin:$kotlin_version-1.0.13"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.devtools.ksp'

repositories {
    mavenCentral()
    google()
    maven { url 'https://jitpack.io' }
    maven { url 'https://developer.huawei.com/repo/' }
}

android {
    compileSdk 33
    defaultConfig {
        targetSdkVersion 33
        versionCode 191
        versionName "3.1." + versionCode
        applicationId "com.juick"
        vectorDrawables.useSupportLibrary true
        multiDexEnabled true
        def addConstant = {constantName, constantValue ->
            manifestPlaceholders += [ (constantName):constantValue]
            buildConfigField "String", "${constantName}", "\"${constantValue}\""
        }
        addConstant("API_ENDPOINT", "https://api.juick.com/")
        addConstant("EVENTS_ENDPOINT", "https://juick.com/api/events")
        addConstant("INTENT_NEW_EVENT_ACTION", "com.juick.NEW_EVENT_ACTION")

        buildConfigField "boolean", "HIDE_NSFW", "false"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testHandleProfiling true
        testFunctionalTest true
    }
    buildFeatures {
        buildConfig true
        viewBinding true
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildTypes {
        debug {
            debuggable true
            multiDexKeepProguard file('multidex-config.pro')
        }
        release {
            namespace "com.juick"
            minifyEnabled false
            /*
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
             */
        }
    }
    flavorDimensions += "notifications"
    productFlavors {
        google {
            dimension "notifications"
            minSdkVersion 19
        }
        huawei {
            dimension "notifications"
            minSdkVersion 17
            buildConfigField "boolean", "HIDE_NSFW", "true"
        }
        free {
            dimension "notifications"
            minSdkVersion 14
        }
    }
    lint {
        abortOnError false
        checkDependencies true
        checkGeneratedSources true
        checkReleaseBuilds false
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "Juick-${variant.flavorName}-v${variant.versionName}.apk"
        }
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}

dependencies {
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.browser:browser:1.5.0'
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    implementation 'com.google.android.material:material:1.9.0'
    googleImplementation 'com.google.android.gms:play-services-auth:20.7.0'
    // Firebase platform
    googleImplementation platform('com.google.firebase:firebase-bom:32.5.0')
    googleImplementation 'com.google.firebase:firebase-messaging'

    // okhttp 3.13+ require API21
    //noinspection GradleDependency
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    //noinspection GradleDependency
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    freeImplementation 'com.github.heremaps:oksse:0.9.0'

    // retrofit 2.7.x require API21
    //noinspection GradleDependency
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    //noinspection GradleDependency
    implementation "com.squareup.retrofit2:converter-jackson:$retrofit_version"
    // Jackson 2.14 requires API26
    //noinspection GradleDependency
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.13.5"

    implementation "com.github.bumptech.glide:glide:$glide_version"
    ksp "com.github.bumptech.glide:ksp:$glide_version"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glide_version"

    implementation 'com.github.vitalyster:ChatKit:5e99a8bf3c'
    huaweiImplementation 'com.huawei.hms:push:6.12.0.300'

    implementation 'com.github.Zhuinden:fragmentviewbindingdelegate-kt:1.0.2'

    implementation 'com.vanniktech:android-image-cropper:4.5.0'

    implementation 'androidx.multidex:multidex:2.0.1'

    // Fragment testing
    debugImplementation "androidx.fragment:fragment-testing:1.6.1"

    // Core library
    androidTestImplementation "androidx.test:core-ktx:$androidx_test_version"

    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.ext:truth:1.5.0'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    // UI Automator
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
}
apply plugin: 'com.google.gms.google-services'
googleServices {
    disableVersionCheck true
}
apply plugin: 'com.huawei.agconnect'
android.applicationVariants.all { variant ->
    project.tasks.getByName('process' + variant.name.capitalize() + 'GoogleServices').enabled variant.name.contains('google')
}
