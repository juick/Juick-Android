buildscript {
    repositories {
        mavenCentral()
        google()
        maven { url 'https://developer.huawei.com/repo/' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
        classpath 'com.google.gms:google-services:4.3.8'
        classpath 'com.huawei.agconnect:agcp:1.5.2.300'
    }
}

apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    google()
    jcenter()
    maven { url 'https://jitpack.io' }
    maven { url 'https://developer.huawei.com/repo/' }
}

android {
    compileSdkVersion 30
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 144
        versionName "3.1"
        applicationId "com.juick"
        vectorDrawables.useSupportLibrary = true
        def addConstant = {constantName, constantValue ->
            manifestPlaceholders += [ (constantName):constantValue]
            buildConfigField "String", "${constantName}", "\"${constantValue}\""
        }
        addConstant("API_ENDPOINT", "https://api.juick.com/")
        addConstant("EVENTS_ENDPOINT", "https://juick.com/api/events")
        addConstant("INTENT_NEW_EVENT_ACTION", "com.juick.NEW_EVENT_ACTION")

        buildConfigField "boolean", "HIDE_NSFW", "false"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testHandleProfiling true
        testFunctionalTest true
    }
    buildFeatures {
        buildConfig = true
        viewBinding = true
    }
    lintOptions {
        checkDependencies = true
        checkGeneratedSources = true
        checkReleaseBuilds = false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError = false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
        release {
            minifyEnabled false
            /*
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
             */
        }
    }
    flavorDimensions "notifications"
    productFlavors {
        google {
            dimension "notifications"
        }
        huawei {
            dimension "notifications"
            minSdkVersion 17
            buildConfigField "boolean", "HIDE_NSFW", "true"
        }
        free {
            dimension "notifications"
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.drawerlayout:drawerlayout:1.1.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.google.android.material:material:1.3.0'
    googleImplementation 'com.google.android.gms:play-services-auth:19.0.0'
    // Firebase platform
    googleImplementation platform('com.google.firebase:firebase-bom:28.2.0')
    googleImplementation 'com.google.firebase:firebase-messaging'

    // okhttp 3.13+ require API21
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:okhttp:3.12.13'
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.13'
    implementation 'com.github.heremaps:oksse:0.9.0'
    // retrofit 2.7.x require API21
    //noinspection GradleDependency
    implementation 'com.squareup.retrofit2:retrofit:2.6.4'
    //noinspection GradleDependency
    implementation 'com.squareup.retrofit2:converter-jackson:2.6.4'
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'androidx.annotation:annotation:1.2.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.12.0@aar'
    implementation 'com.github.stfalcon-studio:Chatkit:0.4.1'
    huaweiImplementation 'com.huawei.hms:push:5.3.0.304'

    // Core library
    androidTestImplementation 'androidx.test:core:1.4.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'

    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.ext:truth:1.4.0'

    // Espresso dependencies
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    // UI Automator
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
}
apply plugin: 'com.google.gms.google-services'
googleServices {
    disableVersionCheck = true
}
apply plugin: 'com.huawei.agconnect'
android.applicationVariants.all { variant ->
    project.tasks.getByName('process' + variant.name.capitalize() + 'GoogleServices').enabled = variant.name.contains('google')
}
