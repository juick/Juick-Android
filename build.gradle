buildscript {
    repositories {
        mavenCentral()
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
        classpath 'com.google.gms:google-services:4.1.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.github.ben-manes.versions'

repositories {
    mavenCentral()
    jcenter()
    google()
}

dependencies {
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:palette-v7:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.google.firebase:firebase-core:16.0.5'
    implementation 'com.google.firebase:firebase-messaging:17.3.4'
    annotationProcessor 'com.bluelinelabs:logansquare-compiler:1.3.7'
    implementation 'com.bluelinelabs:logansquare:1.3.7'
    implementation "com.github.aurae.retrofit2:converter-logansquare:1.4.1"
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'co.lujun:androidtagview:1.1.4'
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.8.0@aar'
    implementation 'com.github.florent37:glidepalette:2.1.2'
    implementation 'com.facebook.rebound:rebound:0.3.8'
    implementation 'com.github.stfalcon:chatkit:0.3.3'
}

android {
    compileSdkVersion "android-28"
    buildToolsVersion '28.0.3'
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 88
        versionName '3.1'
        applicationId "com.juick"
        vectorDrawables.useSupportLibrary = true
        buildConfigField "String", "API_ENDPOINT", '"https://api.juick.com/"'
        buildConfigField "String", "API_ENDPOINT_FALLBACK", '"http://api.juick.com/"'
        buildConfigField "String", "IMAGES_ENDPOINT", '"https://i.juick.com/"'
        buildConfigField "String", "IMAGES_ENDPOINT_FALLBACK", '"http://i.juick.com/"'
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
    }
    // signing config
    if (file("keystore.properties").exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(file("keystore.properties")))

        signingConfigs {
            release {
                storeFile file(props["key.store"])
                storePassword props["key.store.password"]
                keyAlias props["key.alias"]
                keyPassword props["key.alias.password"]
            }
        }

        buildTypes {
            release {
                minifyEnabled false
                signingConfig signingConfigs.release
            }
        }
    }
    buildTypes {
        dev {

        }
    }
    def build_param = "${build}";

    if (build_param != "dev") {
        //exclude production build
        android.variantFilter { variant ->
            if (variant.buildType.name.equals('dev')) {
                variant.setIgnore(true);
            }
        }
    } else {
        //exclude all except production build
        android.variantFilter { variant ->
            if (!variant.buildType.name.equals('dev')) {
                variant.setIgnore(true);
            }
        }
    }
}

apply plugin: "com.google.gms.google-services"
