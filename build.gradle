buildscript {
    ext.kotlin_version = "2.0.21"
    ext.navigation_version = "2.8.7"
    ext.okhttp_version = "4.12.0"
    ext.retrofit_version = "2.11.0"
    ext.androidx_fragment_version = "1.8.5"
    ext.lifecycle_version = "2.8.7"
    ext.compose_bom_version = "2025.01.01"
    ext.androidx_activity_version = "1.10.0"
    ext.androidx_test_version = "1.6.1"

    repositories {
        mavenCentral()
        google()
        maven { url 'https://developer.huawei.com/repo/' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.8.0'
        classpath 'com.google.gms:google-services:4.4.2'
        classpath 'com.huawei.agconnect:agcp:1.9.1.304'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin.plugin.serialization:org.jetbrains.kotlin.plugin.serialization.gradle.plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin.plugin.compose:org.jetbrains.kotlin.plugin.compose.gradle.plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.kotlin.plugin.parcelize'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply plugin: 'org.jetbrains.kotlin.plugin.compose'

repositories {
    mavenCentral()
    google()
    maven { url 'https://jitpack.io' }
    maven { url 'https://developer.huawei.com/repo/' }
}

android {
    compileSdk 35
    defaultConfig {
        targetSdkVersion 35
        minSdkVersion 24
        versionCode 230
        versionName "3.2." + versionCode
        applicationId "com.juick"
        namespace "com.juick"
        vectorDrawables.useSupportLibrary true
        def addConstant = {constantName, constantValue ->
            manifestPlaceholders += [ (constantName):constantValue]
            buildConfigField "String", "${constantName}", "\"${constantValue}\""
        }
        addConstant("API_ENDPOINT", "https://api.juick.com/")
        addConstant("EVENTS_ENDPOINT", "https://juick.com/api/events")
        addConstant("INTENT_NEW_EVENT_ACTION", "com.juick.NEW_EVENT_ACTION")

        buildConfigField "boolean", "HIDE_NSFW", "false"
        buildConfigField "boolean", "ENABLE_COMPOSE_UI", "false"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testHandleProfiling true
        testFunctionalTest true
    }
    buildFeatures {
        buildConfig true
        viewBinding true
        compose true
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
        }
        next {
            initWith debug
            buildConfigField "boolean", "ENABLE_COMPOSE_UI", "true"
        }
    }
    flavorDimensions += "notifications"
    productFlavors {
        google {
            dimension "notifications"
        }
        huawei {
            dimension "notifications"
            buildConfigField "boolean", "HIDE_NSFW", "true"
        }
        free {
            dimension "notifications"
        }
    }
    lint {
        abortOnError false
        checkDependencies true
        checkGeneratedSources true
        checkReleaseBuilds false
    }
    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            outputFileName = "Juick-${variant.flavorName}-v${variant.versionName}.apk"
        }
        variant.resValue "string", "applicationId", "${applicationId}"
        variant.resValue "string", "messages_provider_authority", "${applicationId}.messages"
        project.tasks.getByName('process' + variant.name.capitalize() + 'GoogleServices').enabled variant.name.contains('google')
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}

dependencies {
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.7.3'
    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.browser:browser:1.8.0'
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"
    implementation "androidx.savedstate:savedstate-ktx:1.2.1"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    implementation 'com.google.android.material:material:1.12.0'

    // Sign in with Google
    googleImplementation "androidx.credentials:credentials-play-services-auth:1.3.0"
    googleImplementation 'com.google.android.gms:play-services-auth:21.3.0'
    googleImplementation "com.google.android.libraries.identity.googleid:googleid:1.1.1"

    // Firebase platform
    googleImplementation platform('com.google.firebase:firebase-bom:33.9.0')
    googleImplementation 'com.google.firebase:firebase-messaging'

    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    freeImplementation 'com.github.heremaps:oksse:0.9.0'

    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-kotlinx-serialization:$retrofit_version"

    implementation 'com.github.vitalyster:ChatKit:5e99a8bf3c'
    huaweiImplementation 'com.huawei.hms:push:6.13.0.300'

    implementation 'com.github.Zhuinden:fragmentviewbindingdelegate-kt:1.0.2'

    implementation 'com.vanniktech:android-image-cropper:4.6.0'

    // Compose
    nextImplementation "androidx.activity:activity-compose:$androidx_activity_version"
    nextImplementation "androidx.constraintlayout:constraintlayout-compose:1.1.0"
    nextImplementation platform("androidx.compose:compose-bom:$compose_bom_version")
    nextImplementation 'androidx.compose.ui:ui'
    nextImplementation 'androidx.compose.ui:ui-graphics'
    nextImplementation 'androidx.compose.ui:ui-viewbinding'
    nextImplementation 'androidx.compose.ui:ui-tooling-preview'
    nextImplementation 'androidx.compose.material:material-icons-extended'
    nextImplementation 'androidx.compose.material3:material3'
    nextImplementation 'androidx.compose.ui:ui-tooling'

    // Fragment testing
    debugImplementation "androidx.fragment:fragment-testing-manifest:$androidx_fragment_version"

    // Core library
    androidTestImplementation "androidx.test:core-ktx:$androidx_test_version"

    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.ext:truth:1.6.0'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation("androidx.fragment:fragment-testing:$androidx_fragment_version")
    // UI Automator
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
}
apply plugin: 'com.google.gms.google-services'
googleServices {
    disableVersionCheck true
}
apply plugin: 'com.huawei.agconnect'
