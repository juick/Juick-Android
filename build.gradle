buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.github.ben-manes.versions'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.neovisionaries:nv-websocket-client:2.3'
    // Play services 10.0.x is the final release that includes full support for Android version 2.3.x
    //noinspection GradleDependency
    compile 'com.google.android.gms:play-services-gcm:10.0.1'
    apt 'com.bluelinelabs:logansquare-compiler:1.3.7'
    compile 'com.bluelinelabs:logansquare:1.3.7'
    compile "com.github.aurae.retrofit2:converter-logansquare:1.4.1"
    compile 'com.squareup.okhttp3:okhttp:3.9.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'co.lujun:androidtagview:1.1.4'
    // this is the latest glide with android 2.3 support
    //noinspection GradleDependency
    compile 'com.github.bumptech.glide:glide:3.8.0'
    compile 'com.github.bumptech.glide:okhttp3-integration:1.5.0@aar'
    compile 'com.facebook.rebound:rebound:0.3.8'
    compile 'de.hdodenhof:circleimageview:2.1.0'
}

android {
    compileSdkVersion "android-25"
    buildToolsVersion '25.0.3'
    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 25
        versionCode 60
        versionName '3.1'
        applicationId "com.juick"
        buildConfigField "String", "API_ENDPOINT", '"https://api.juick.com/"'
        buildConfigField "String", "API_ENDPOINT_FALLBACK", '"http://api.juick.com/"'
        buildConfigField "String", "IMAGES_ENDPOINT", '"https://i.juick.com/"'
        buildConfigField "String", "IMAGES_ENDPOINT_FALLBACK", '"http://i.juick.com/"'
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
    }
    // signing config
    if (file("keystore.properties").exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(file("keystore.properties")))

        signingConfigs {
            release {
                storeFile file(props["key.store"])
                storePassword props["key.store.password"]
                keyAlias props["key.alias"]
                keyPassword props["key.alias.password"]
            }
        }

        buildTypes {
            release {
                minifyEnabled false
                signingConfig signingConfigs.release
            }
        }
    }
}
